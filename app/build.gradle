apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def getVersionCode = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', "HEAD"
        standardOutput = stdout
    }
    return Integer.parseInt(stdout.toString().trim())
}

buildscript {
    ext.kotlin_version = '1.2.31'
    ext.anko_version = '0.10.4'
    ext.support_version = '27.+'
    repositories {
        maven { url "https://maven.google.com" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.anko:anko-common:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk25:$anko_version"
    // implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"

    compileOnly files('lib/XposedBridgeApi-54.jar')

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:preference-v14:$support_version"
    // compile 'com.android.support:support-vector-drawable:27.+'

    // implementation 'com.fasterxml.jackson.jr:jackson-jr-objects:2.9.0.pr3'

    // Color Picker
    // implementation 'com.jaredrummler:colorpicker:1.0.2'
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "me.manhong2112.mimikkouimod"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName "1.0b"
    }
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        release {
            setV1SigningEnabled true
            setV2SigningEnabled true
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            setKeyAlias properties.getProperty('keyAlias')
            setKeyPassword properties.getProperty('keyPassword')
            setStoreFile file(properties.getProperty('storeFile'))
            setStorePassword properties.getProperty('storePassword')
        }
    }
    buildTypes {
        release {
            setMinifyEnabled true
            setZipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            setSigningConfig signingConfigs.release
        }
    }
}
